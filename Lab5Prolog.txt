vars(F, [F]) :- atom(F).
vars(non(F), L) :- vars(F, L).
vars(si(F1, F2), L) :- vars(F1, L1), vars(F2, L2), 
    union(L1, L2, L).
vars(imp(F1, F2), L) :- vars(F1, L1), vars(F2, L2), 
    union(L1, L2, L).
vars(sau(F1, F2), L) :- vars(F1, L1), vars(F2, L2), 
    union(L1, L2, L).

val(V, [(V, X)|_], X).
val(V, [_|T], X) :- val(V, T, X).

bnon(1, 0).
bnon(0, 1).
bsi(0, _, 0).
bsi(_, 0, 0).
bsi(1, 1, 1).
bsau(1, _, 1).
bsau(_, 1, 1).
bsau(0, 0, 0).
bimp(X, Y, Z) :- bnon(X, N), bsau(N, Y, Z).

eval(V, E, A) :- atom(V), val(V, E, A).
eval(non(F), E, A) :- eval(F, E, A1), bnon(A1, A).
eval(imp(F1, F2), E, A) :- eval(F1, E, A1), eval(F2, E, A2),
    bimp(A1, A2, A).
eval(si(F1, F2), E, A) :- eval(F1, E, A1), eval(F2, E, A2),
    bsi(A1, A2, A).
eval(sau(F1, F2), E, A) :- eval(F1, E, A1), eval(F2, E, A2),
    bsau(A1, A2, A).

evals(_, [], []).
evals(X, [E|T1], [A|T2]) :- eval(X, E, A), evals(X, T1, T2).

evs([], [[]]).
evs([X|T], Es) :- evs(T, Ls), add(X, Ls, Es).
add(_, [], []).
add(X, [E|T], [[(X, 0)|E], [(X, 1)|E]|Es]) :- add(X, T, Es). 

all_evals(X, As) :- vars(X, Vs), evs(Vs, Es), evals(X, Es, As).
                      
taut(X) :- all_evals(X, As), all_ones(As).
all_ones([]).
all_ones([1|T]) :- all_ones(T).